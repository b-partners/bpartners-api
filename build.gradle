buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:5.3.0"
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.5.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'

    id 'org.openapi.generator' version '5.3.0'

    id 'org.sonarqube' version '3.3'
    id 'jacoco'
    id 'checkstyle'
}

group = 'app.bpartners'
sourceCompatibility = "11"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

task generateJavaClient(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "java"
    inputSpec = "$rootDir/doc/api.yml".toString()
    outputDir = "$buildDir/gen".toString()
    apiPackage = "app.bpartners.api.endpoint.rest.api"
    invokerPackage = "app.bpartners.api.endpoint.rest.client"
    modelPackage = "app.bpartners.api.endpoint.rest.model"

    configOptions = [
            serializationLibrary: "jackson",
            dateLibrary         : "custom"
    ]
    typeMappings = [
            // What date-time type to use when? https://i.stack.imgur.com/QPhGW.png
            Date    : "java.time.LocalDate",
            DateTime: "java.time.Instant",
    ]
    library = "native"

    groupId = 'app.bpartners'
    id = 'api-rest'
    skipValidateSpec = false
    logToStderr = true
    generateAliasAsModel = false
    enablePostProcessFile = false
}
task generateTsClient(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "typescript-axios"
    inputSpec = "$rootDir/doc/api.yml".toString()
    outputDir = "$buildDir/gen-ts".toString()
}
task publishJavaClientToMavenLocal(type: Exec, dependsOn: generateJavaClient) {
    if (org.apache.tools.ant.taskdefs.condition.Os.isFamily(org.apache.tools.ant.taskdefs.condition.Os.FAMILY_WINDOWS)) {
        commandLine './publish_gen_to_maven_local.bat'
    } else {
        commandLine './publish_gen_to_maven_local.sh'
    }
}
tasks.compileJava.dependsOn publishJavaClientToMavenLocal

apply plugin: 'checkstyle'
checkstyle {
    toolVersion '8.45'
    configFile file("config/checkstyle/google_checks_custom.xml")
}
checkstyleMain {
    source = 'src/main/java'
}
checkstyleTest {
    source = 'src/test/java'
}
tasks.withType(Checkstyle) {
    reports {
        xml.enabled true
    }
}

ext {
    set('testcontainersVersion', "1.15.3")
}
test {
    exclude '**/FileIT.class'
    exclude '**/InvoiceIT.class'
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

dependencies {
    implementation 'app.bpartners:api-rest:latest'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'com.sendinblue:sib-api-v3-sdk:6.0.0'

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.nimbusds:nimbus-jose-jwt'

    implementation 'com.vladmihalcea:hibernate-types-52:1.0.0'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.postgresql:postgresql'

    implementation 'software.amazon.awssdk:s3-transfer-manager:2.17.271-PREVIEW'
    implementation 'software.amazon.awssdk:eventbridge:2.17.109'
    implementation 'software.amazon.awssdk:sqs:2.17.109'
    implementation 'software.amazon.awssdk:cognitoidentityprovider:2.17.109'
    implementation 'software.amazon.awssdk:ssm:2.17.109'
    implementation 'software.amazon.awssdk:transfer:2.17.271'
    annotationProcessor 'software.amazon.awssdk:s3:2.17.271'

    implementation 'io.sentry:sentry-spring-boot-starter:5.5.3'
    implementation 'io.sentry:sentry-logback:5.5.3'

    implementation 'org.thymeleaf:thymeleaf:3.0.15.RELEASE'
    implementation 'org.xhtmlrenderer:flying-saucer-pdf:9.1.22'

    implementation 'com.google.zxing:core:3.5.0'
    implementation 'com.google.zxing:javase:3.5.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    testImplementation 'com.github.javafaker:javafaker:1.0.2'

    testImplementation 'com.amazonaws:aws-java-sdk-s3:1.12.315'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:localstack'

}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}